#!/usr/bin/perl -Tw
#
# Copyright (c) 2001-2003 Gregory M. Kurtzer
# 
# Copyright (c) 2003-2011, The Regents of the University of California,
# through Lawrence Berkeley National Laboratory (subject to receipt of any
# required approvals from the U.S. Dept. of Energy).  All rights reserved.
#



use Warewulf::Logger;
use Warewulf::Term;
use Warewulf::DataStore;
use Warewulf::Config;
use Warewulf::ModuleLoader;
use Warewulf::EventHandler;
use Warewulf::Debug;
use Getopt::Long;
use Term::ReadLine;
use File::Path;
use Text::ParseWords;

my ($modules, $db, $term);
my @keywords;
my %keyword_hash;
my $retval = 0;
my $events = Warewulf::EventHandler->new();

$ENV{"PATH"} = "/bin:/usr/bin";

sub
run_cmd()
{
    my ($command, @ARGS) = @_;

    if ($command) {
        my ($keyword, @arg) = split(/\s+/, $command, 2);
        my $run_modules = 0;

        push(@arg, @ARGS);

        if ($keyword) {
            foreach my $module ($modules->list($keyword)) {
                if ($module->can("exec")) {
                    $run_modules++;
                    if (! $module->exec(@arg) ) {
                        $retval++;
                    }
                } else {
                    &iprint("Warewulf module '$keyword' does not support exec()\n");
                }
            }
        }
        &dprint("Command executed by $run_modules modules\n");
    }
}


######################################################################

my $opt_debug;
my $opt_verbose;
my $opt_quiet;
my $opt_noask;

Getopt::Long::Configure ("bundling", "passthrough");

GetOptions(
    'debug'         => \$opt_debug,
    'verbose'       => \$opt_verbose,
    'quiet'         => \$opt_quiet,
    'noask'         => \$opt_noask,
);


&set_log_level("NOTICE");

if ($opt_debug) {
    &set_log_level("DEBUG");
} elsif ($opt_verbose) {
    &set_log_level("INFO");
} elsif ($opt_quiet) {
    $term->interactive(0);
    &set_log_level("WARNING");
}

$modules = Warewulf::ModuleLoader->new("Cli");
$db = Warewulf::DataStore->new();
$term = Warewulf::Term->new();

foreach my $module ($modules->list()) {
    push(@keywords, $module->keyword());
    $term->complete($module->keyword(), $module);
}

# Make sure all keywords are unique
%keyword_hash = map { $_, 1 } @keywords;
@keywords = sort keys %keyword_hash;


if (exists($ENV{"HOME"}) and $ENV{"HOME"} =~ /^([a-zA-Z0-9\/\._\-]+)$/) {
    my $home = $1;
    if (! -d "$home/.wwsh") {
        mkpath("$home/.wwsh");
    }
}

$events->eventloader();
$events->handle("WWSH.START");

if (exists($ARGV[0]) and -f $ARGV[0]) {
    &iprint("Running non-interactively from file\n");
    $term->interactive(0);
    open(INPUT, $ARGV[0]);
    while(my $line = <INPUT>) {
        chomp($line);

        &run_cmd(&quotewords('\s+', 1, $line));
    }
} elsif (@ARGV) {
    if ($opt_noask) {
        $term->interactive(0);
    }
    &iprint("Running non-interactively from directly passed argument\n");
    &run_cmd(@ARGV);
} elsif ($term->interactive()) {
    $term->history_load("$ENV{HOME}/.wwsh/history");

    while ( defined ($_ = $term->get_input((exists($set{"CMD"}) ? "Warewulf > $set{CMD}: " : "Warewulf> ")))) {
        chomp;
        if ($_ =~ /^([^=^ ]+)=(.+)$/) {
            my $key = uc($1);
            my $val = $2;
            if ($key and $val) {
                $set{"$key"} = $val;
                &dprint("WWSH SET: $key = $val\n");
            } else {
                &eprint("Both key and value must be defined\n");
            }
        } elsif ($_ =~ /^unset (.+)$/) {
            my $key = uc($1);
            if (exists($set{"$key"})) {
                delete($set{"$key"});
            }
        } elsif ($_ eq "..") {
            if (exists($set{"CMD"})) {
                delete($set{"CMD"});
            }
        } else {
            my $key;
            if ($_) {
                ($key) = split(/\s+/, $_, 0);
                if (! exists($keyword_hash{"$key"}) and exists($set{"CMD"})) {
                    $_ = join(" ", $set{"CMD"}, $_);
                }
            } elsif (exists($set{"CMD"})) {
                $_ = $set{"CMD"};
            }

            if ($_) {
                ($key) = split(/\s+/, $_, 0);
                $term->history_add($_);
                if (exists($keyword_hash{"$key"})) {
                    &run_cmd(&quotewords('\s+', 1, $_));
                } else {
                    &eprint("Unknown command\n");
                }
            }
        }
    }

    $term->history_save();
    print "\n";
} else {
    &iprint("Running non-interactively\n");
    $term->interactive(0);
    while (<>) {
        my $line;

        chomp($line = $_);
        &run_cmd($line);
    }
}


$events->handle("WWSH.END");
exit($retval);
